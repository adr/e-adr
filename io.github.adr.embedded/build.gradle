plugins {
  id 'java-library'
  // maven-publish is the successor of maven plugin
  id 'maven-publish'
  id 'signing'
}

jar {
  manifest {
    attributes('Automatic-Module-Name': 'io.github.adr.embedded')
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'e-adr'
      from components.java
      pom {
        name ='Embedded Architectural Decision Records'
        description = 'Architectural Decision Records embedded in Java code'
        url = 'https://github.com/adr/e-adr/'

        licenses {
          license {
            name = 'Eclipse Public License - v 2.0'
            url = 'https://www.eclipse.org/org/documents/epl-2.0/'
          }
        }
        developers {
          developer {
            id = 'koppor'
            name = 'Oliver Kopp'
            email = 'kopp.dev@gmail.com'
          }
        }
        scm {
          url = 'https://github.com/adr/e-adr'
          connection = 'scm:git:https://github.com/adr/e-adr.git'
          developerConnection = 'scm:git:github.com:adr/e-adr.git'
        }
      }
    }
  }
}

signing {
  required = isReleaseVersion
  useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSWORD"))
  sign publishing.publications.mavenJava
}

nexusPublishing {
  repositories {
    sonatype {
      username = System.getenv("MAVEN_USERNAME")
      password = System.getenv("MAVEN_PASSWORD")
      nexusUrl.set(uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/"))
      snapshotRepositoryUrl.set(uri("https://oss.sonatype.org/content/repositories/snapshots/"))
    }
  }
}
